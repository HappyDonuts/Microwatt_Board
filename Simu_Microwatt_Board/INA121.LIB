

* INA121
*$
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: INA121
* Date: 08JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE 
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS078
*
* Model Version: 1.0
*
*****************************************************************************
* 
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* COMMENTS
* CONNECTIONS:          NON-INVERTING INPUT
*                        |  INVERTING INPUT
*                        |   |  POSITIVE POWER SUPPLY
*                        |   |   |  NEGATIVE POWER SUPPLY
*                        |   |   |   |  OUTPUT
*                        |   |   |   |   |  REFERENCE
*                        |   |   |   |   |   |  GAIN SENSE 1
*                        |   |   |   |   |   |   |  GAIN SENSE 2
*                        |   |   |   |   |   |   |   |
* PIN CONFIG FOR INA121  1   2   3   4   5   8   9  10
*****************************************************************************
.SUBCKT INA121         1   2   3   4   5   8   9  10
 X1                    15  17   3   4  11   A1_121E
 X2                    15  16   3   4  12   A2_121E
 X3                    14  13   3   4   5   A3_121E
 CIN   13  14   4PF		
 C4    14   8   .50000E-12	
 CC1   17  11   11.00E-12
 CC2   16  12   11.00E-12
 CG1    9   0   11.500E-12	
 CG2   10   0   10.000E-12			
 R1    11  13   40.0000E3
 R2    13   5   39.996E3
 R3    12  14   40.0000E3
 R4    14   8   40.0000E3
 R1FB   9  11   25.0000E3
 R2FB  10  12   25.0000E3
 RCE   17   9   500E6		
 I1     3  16  DC  10.00E-6
 I2     3  17  DC  10.00E-6
 I3    10   4  DC  10.00E-6
 I4     9   4  DC  10.00E-6
 I5     3  21  DC  30.00E-6
 I6     3  22  DC  30.00E-6
 Q1    16  21  10  QX
 Q2    17  22   9  QX
 D1    17  15      DX
 D2    16  15      DX
 D12   15  17	  DX
 D22   15  16	  DX
 J1     4  31  21  JX
 J2     4  33  22  JX
 G11  31 4 POLY(2) (21,1) (4,1) 28E-12 3E-12 4.2E-12
 G21  33 4 POLY(2) (22,2) (4,2) 28E-12 3E-12 4.2E-12
 V1     3  15  DC  1.000
* POS INPUT PROTECTION
 RINP 1    30   10
 J1P  31   32   30  jy 15x
 J2P  32   32   30  jz 1x
 D1P  31   3    dx
 D2P  4    31   dx
* NEG INPUT PROTECTION
 RINN 2    35   10
 J1N  33   34   35  jy 15x
 J2N  34   34   35  jz 1x
 D1N  33   3    dx
 D2N  4    33   dx
.MODEL DX D(IS=1.0E-24)
.MODEL QX NPN(IS=800.0E-18 BF=500)
.MODEL JX PJF(IS=1E-12 BETA=750.0E-6 VTO=-1)
.MODEL JY PJF(IS=800.0E-18 VTO=-1.2 BETA=10U LAMBDA=13M)
.MODEL JZ PJF(IS=800.0E-18 VTO=-1.2 BETA=1U LAMBDA=13M)
.ENDS INA121

* A1_121E operational amplifier "macromodel" subcircuit
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.subckt A1_121E  1 2 3 4 5
*
  c1   11 12 2.8868E-12
  c2    6  7 10.000E-12
  css  10 99 1.0000E-30
  dc    5 53 dy
  de   54  5 dy
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 17.684E6 -1E3 1E3 18E6 -18E6
  ga    6  0 11 12 1.2566E-3
  gcm   0  6 10 99 12.566E-9
  iss   3 10 dc 12.000E-6
  hlim 90  0 vlim 1K
  j1   11  2 10 jx1
  j2   12  1 10 jx2
  r2    6  9 100.00E3
  rd1   4 11 795.77
  rd2   4 12 795.77
  ro1   8  5 1
  ro2   7 99 9
  rp    3  4 3E6
  rss  10 99 16.667E6
  vb    9  0 dc 0
  vc    3 53 dc 1.6621
  ve   54  4 dc 1.0621
  vlim  7  8 dc 0
  vlp  91  0 dc 5
  vln   0 92 dc 5
* OUTPUT SUPPLY MIRROR
  FQ3   0 20 POLY(1) VLIM 0 1
  DQ1  20 21 DX
  DQ2  22 20 DX
  VQ1  21  0 0
  VQ2  22  0 0 
  FQ1   3  0 POLY(1) VQ1 70u 1
  FQ2   0  4 POLY(1) VQ2 70u -1
.model dx D(Is=800.00E-18)
.model dy D(Is=800.00E-18 Rs=1m Cjo=10p)
.model jx1 PJF(Is=1.5000E-12 Beta=.13159 Vto=-1)
.model jx2 PJF(Is=1.5000E-12 Beta=.13159 Vto=-1)
.ends

* A2_121E operational amplifier "macromodel" subcircuit
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.subckt A2_121E  1 2 3 4 5
*
  c1   11 12 2.8868E-12
  c2    6  7 10.000E-12
  css  10 99 1.0000E-30
  dc    5 53 dy
  de   54  5 dy
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 17.684E6 -1E3 1E3 18E6 -18E6
  ga    6  0 11 12 1.2566E-3
  gcm   0  6 10 99 12.566E-9
  iss   3 10 dc 12.000E-6
  hlim 90  0 vlim 1K
  j1   11  2 10 jx1
  j2   12  1 10 jx2
  r2    6  9 100.00E3
  rd1   4 11 795.77
  rd2   4 12 795.77
  ro1   8  5 1
  ro2   7 99 9
  rp    3  4 3E6
  rss  10 99 16.667E6
  vb    9  0 dc 0
  vc    3 53 dc 1.6621
  ve   54  4 dc 1.0621
  vlim  7  8 dc 0
  vlp  91  0 dc 5
  vln   0 92 dc 5
* OUTPUT SUPPLY MIRROR
  FQ3   0 20 POLY(1) VLIM 0 1
  DQ1  20 21 DX
  DQ2  22 20 DX
  VQ1  21  0 0
  VQ2  22  0 0 
  FQ1   3  0 POLY(1) VQ1 70u 1
  FQ2   0  4 POLY(1) VQ2 70u -1
.model dx D(Is=800.00E-18)
.model dy D(Is=800.00E-18 Rs=1m Cjo=10p)
.model jx1 PJF(Is=1.5000E-12 Beta=.13159 Vto=-1)
.model jx2 PJF(Is=1.5000E-12 Beta=.13159 Vto=-1)
.ends

* A3_121E operational amplifier "macromodel" subcircuit
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.subckt A3_121E  1 2 3 4 5
*
  c1   11 12 1.6246E-12
  c2    6  7 10.000E-12
  cee  10 99 714.29E-15
  dc    5 53 dy
  de   54  5 dy
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 1.3263E9 -1E3 1E3 1E9 -1E9
  ga    6  0 11 12 50.265E-6
  gcm   0  6 10 99 502.65E-12
  iee   3 10 dc 7.5400E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx1
  q2   12  1 14 qx2
  r2    6  9 100.00E3
  rc1   4 11 19.894E3
  rc2   4 12 19.894E3
  re1  13 10 12.928E3
  re2  14 10 12.928E3
  ree  10 99 26.525E6
  ro1   8  5 1
  ro2   7 99 9
  rp    3  4 3E6
  vb    9  0 dc 0
  vc    3 53 dc 1.6905
  ve   54  4 dc 1.0905
  vlim  7  8 dc 0
  vlp  91  0 dc 15
  vln   0 92 dc 15
*OUTPUT SUPPLY MIRROR
  FQ3   0 20 POLY(1) VLIM 0 1
  DQ1  20 21 DX
  DQ2  22 20 DX 
  VQ1  21  0 0
  VQ2  22  0 0 
  FQ1   3  0 POLY(1) VQ1 150U 1
  FQ2   0  4 POLY(1) VQ2 150U -1
.model dx D(Is=800.00E-18)
.model dy D(Is=800.00E-18 Rs=1m Cjo=10p)
.model qx1 PNP(Is=800.00E-18 Bf=187.50)
.model qx2 PNP(Is=800.00E-18 Bf=187.50)
.ends
*$